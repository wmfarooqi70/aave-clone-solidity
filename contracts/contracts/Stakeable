// SPDX-License-Identifier: MIT
pragma solidity ^0.8.4;

/**
 * @notice Stakeable is a contract who is ment to be inherited by other contract that wants Staking capabilities
 */
contract Stakeable {
    constructor() {
        activeStakeholders.push();
    }

    struct ActiveStake {
        address user;
        uint256 amount;
        uint256 since;
        uint256 claimable;
    }

    struct ActiveStakeholder {
        address user;
        ActiveStake[] active_stake_adderesses;
    }

    struct StakingHistory {
        uint256 total_amount;
        ActiveStake[] stakes;
    }

    mapping(address => uint256) internal stakingHistoryStakesMapToIndex;

    ActiveStakeholder[] internal activeStakeholders;

    event Staked(
        address indexed user,
        uint256 amount,
        uint256 index,
        uint256 timestamp
    );

    /**
     * @notice
      rewardPerHour is 1000 because it is used to represent 0.001, since we only use integer numbers
      This will give users 0.1% reward for each staked token / H
     */
    uint256 internal rewardPerHour = 1000;

    /**
     * _addStakeholder takes care of adding a stakeholder to the activeStakeholders array
     */
    function _addStakeholder(address staker) internal returns (uint256) {
        // @TODO rewrite this, first declare object and then push
        activeStakeholders.push();
        uint256 userIndex = activeStakeholders.length - 1;
        activeStakeholders[userIndex].user = staker;
        stakingHistoryStakesMapToIndex[staker] = userIndex;
        return userIndex;
    }

    /**
     * @notice
     * _Stake is used to make a stake for an sender. It will remove the amount staked from the stakers account and place those tokens inside a stake container
     * StakeID
     */
    function _stake(uint256 _amount) internal {
        // Simple check so that user does not stake 0
        require(_amount > 0, "Cannot stake nothing");

        // Mappings in solidity creates all values, but empty, so we can just check the address
        uint256 index = stakingHistoryStakesMapToIndex[msg.sender];
        // block.timestamp = timestamp of the current block in seconds since the epoch
        uint256 timestamp = block.timestamp;
        // See if the staker already has a staked index or if its the first time
        if (index == 0) {
            // This stakeholder stakes for the first time
            // We need to add him to the stakeHolders and also map it into the Index of the stakes
            // The index returned will be the index of the stakeholder in the activeStakeholders array
            index = _addStakeholder(msg.sender);
        }

        // Use the index to push a new ActiveStake
        // push a newly created ActiveStake with the current block timestamp.
        activeStakeholders[index].active_stake_adderesses.push(
            ActiveStake(msg.sender, _amount, timestamp, 0)
        );
        // Emit an event that the stake has occured
        emit Staked(msg.sender, _amount, index, timestamp);
    }

    /**
     * @notice
     * calculateStakeReward is used to calculate how much a user should be rewarded for their stakes
     * and the duration the stake has been active
     */
    function calculateStakeReward(ActiveStake memory _current_stake)
        internal
        view
        returns (uint256)
    {
        // First calculate how long the stake has been active
        // Use current seconds since epoch - the seconds since epoch the stake was made
        // The output will be duration in SECONDS ,
        // We will reward the user 0.1% per Hour So thats 0.1% per 3600 seconds
        // the alghoritm is  seconds = block.timestamp - stake seconds (block.timestap - _stake.since)
        // hours = Seconds / 3600 (seconds /3600) 3600 is an variable in Solidity names hours
        // we then multiply each token by the hours staked , then divide by the rewardPerHour rate
        return
            (((block.timestamp - _current_stake.since) / 1 hours) *
                _current_stake.amount) / rewardPerHour;
    }

    /**
     * @notice
     * withdrawStake takes in an amount and a index of the stake and will remove tokens from that stake
     * Notice index of the stake is the users stake counter, starting at 0 for the first stake
     * Will return the amount to MINT onto the acount
     * Will also calculateStakeReward and reset timer
     */
    function _withdrawStake(uint256 amount, uint256 index)
        internal
        returns (uint256)
    {
        // Grab user_index which is the index to use to grab the ActiveStake[]
        uint256 user_index = stakingHistoryStakesMapToIndex[msg.sender];
        ActiveStake memory current_stake = activeStakeholders[user_index]
            .active_stake_adderesses[index];
        require(
            current_stake.amount >= amount,
            "Staking: Cannot withdraw more than you have staked"
        );

        // Calculate available Reward first before we start modifying data
        uint256 reward = calculateStakeReward(current_stake);
        // Remove by subtracting the money unstaked
        current_stake.amount = current_stake.amount - amount;
        // If stake is empty, 0, then remove it from the array of stakes
        if (current_stake.amount == 0) {
            delete activeStakeholders[user_index].active_stake_adderesses[index];
        } else {
            // If not empty then replace the value of it
            activeStakeholders[user_index]
                .active_stake_adderesses[index]
                .amount = current_stake.amount;
            // Reset timer of stake
            activeStakeholders[user_index].active_stake_adderesses[index].since = block
                .timestamp;
        }

        return amount + reward;
    }

    /**
     * @notice
     * hasStake is used to check if a account has stakes and the total amount along with all the seperate stakes
     */
    function hasStake(address _staker)
        public
        view
        returns (StakingHistory memory)
    {
        // totalStakeAmount is used to count total staked amount of the address
        uint256 totalStakeAmount;
        // Keep a summary in memory since we need to calculate this
        StakingHistory memory summary = StakingHistory(
            0,
            activeStakeholders[stakingHistoryStakesMapToIndex[_staker]].active_stake_adderesses
        );
        // Itterate all stakes and grab amount of stakes
        for (uint256 s = 0; s < summary.stakes.length; s += 1) {
            uint256 availableReward = calculateStakeReward(summary.stakes[s]);
            summary.stakes[s].claimable = availableReward;
            totalStakeAmount = totalStakeAmount + summary.stakes[s].amount;
        }
        // Assign calculate amount to summary
        summary.total_amount = totalStakeAmount;
        return summary;
    }
}
